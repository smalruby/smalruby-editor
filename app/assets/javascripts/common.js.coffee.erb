window.SmalrubyEditor = {}
window.changed = false
window.textEditor = null
window.blockMode = true

window.successMessage = (title, msg = '', selector = '#messages') ->
  html = $('<div class="alert alert-success" style="display: none">')
    .append("<h4><i class=\"icon-star\"></i>#{title}</h4>")
    .append(msg)
  $(selector).append(html)
  html.fadeIn('slow')
    .delay(3000)
    .fadeOut('slow')

window.errorMessage = (msg, selector = '#messages') ->
  html = $('<div class="alert alert-error" style="display: none">')
    .append('<button type="button" class="close" data-dismiss="alert">×</button>')
    .append('<h4><i class="icon-exclamation-sign"></i>エラー</h4>')
    .append(msg)
  $(selector).append(html)
  html.fadeIn('slow')

$ ->
  saving = false

  getSourceCodeData = ->
    window.textEditor.getSession().getDocument().getValue()

  setSourceCodeData = (data) ->
    window.textEditor.getSession().getDocument().setValue(data)


  $('a[data-toggle="tab"]').on 'shown', (e) ->
    if $(e.target).attr('href') == '#block-tab'
      window.blockMode = true
    else
      window.blockMode = false
      data = Blockly.Ruby.workspaceToCode()
      if $.trim(data).length > 0
        setSourceCodeData(data)
        window.textEditor.moveCursorTo(0, 0)
      window.textEditor.focus()

  $('#save-button').click (e) ->
    e.preventDefault()
    filename = $.trim($('#filename').val())
    if filename.length <= 0
      window.errorMessage('セーブする前にプログラムに名前をつけてね！')
      $('#filename').focus()
    else
      afterSave = ->
        window.changed = false
        window.successMessage('セーブしました')

        $.ajax
          url: '/source_codes/check'
          type: 'POST'
          data:
            source_code:
              data: getSourceCodeData()
          dataType: 'json'
          success: (data, textStatus, jqXHR) ->
            if data.length == 0
              window.successMessage('チェックしました', 'ただし、プログラムを動かすとエラーが見つかるかもしれません。')
            else
              for errorInfo in data
                do (errorInfo) ->
                  msg = "#{errorInfo.row}行"
                  if errorInfo.column > 0
                    msg += "、#{errorInfo.column}文字"
                  window.errorMessage(msg + ": #{errorInfo.message}")

      $.ajax
        url: '/source_codes/'
        type: 'POST'
        data:
          source_code:
            filename: filename
            data: getSourceCodeData()
        dataType: 'json'
        success: (data, textStatus, jqXHR) ->
          <% if Rails.env == 'standalone' %>
          $.ajax
            url: '/source_codes/write'
            type: 'DELETE'
            dataType: 'json'
            success: (data, textStatus, jqXHR) ->
              if data.source_code.error
                if confirm("前に#{filename}という名前でセーブしているけど本当にセーブしますか？\nセーブすると前に作成したプログラムは消えてしまうよ！")
                  $.ajax
                    url: '/source_codes/write?force=1'
                    type: 'DELETE'
                    dataType: 'json'
                    success: (data, textStatus, jqXHR) ->
                      afterSave()
                else
                  window.successMessage('セーブをキャンセルしました')
              else
                afterSave()
          <% else %>
          afterSave()
          saving = true
          $('#download-link').click()
          <% end %>

  $('#filename').keypress (e) ->
    e = window.event if !e
    if e.keyCode == 13
      $('#save-button').click()
      false
    else
      true

  $('#load-button').click (e) ->
    e.preventDefault()
    if window.changed
      return unless confirm('まだセーブしていないのでロードするとプログラムが消えてしまうよ！\nそれでもロードしますか？')
    $(@).parent().find('input[name="source_code[file]"]').click()

  $('#file-form').fileupload
    dataType: 'json'
    done: (e, data) ->
      info = data.result.source_code
      if info.error
        window.errorMessage(info.filename + 'は' + info.error)
      else
        $('#filename').val(info.filename)
        setSourceCodeData(info.data)
        window.textEditor.moveCursorTo(0, 0)
        unless window.blockMode
          window.textEditor.focus()
        window.changed = false
        window.successMessage('ロードしました')

  window.onbeforeunload = (event) ->
    if !saving && getSourceCodeData().length > 0
      return '作成中のプログラムが消えてしまうよ！'
    else
      saving = false
      return
